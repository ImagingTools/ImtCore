#! this is a configuration file for SDL-based projects
#! \USAGE:
#!	Declare a vatiable with schemas SDL_SCHEMES_LIST ##< it is a list of schemas to process
#!	Include this file include($$PWD/../../../Config/QMake/SdlConfiguration.pri)

################################# General #################################

CONFIG -= qt
INCLUDEPATH += $$[QT_INSTALL_HEADERS]

win32{
	QMAKE_RCC = rcc.exe
	COPY_FILE = copy
	SDL_GENERATOR_BIN = SdlCodeGenerator.exe
}
else{
	QMAKE_RCC = rcc
	COPY_FILE = cp
	SDL_GENERATOR_BIN = SdlCodeGenerator
}
macx*{
	SDL_GENERATOR_BIN = SdlCodeGenerator.app/Contents/MacOS/SdlCodeGenerator
}

isEmpty(SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS) {
	SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS = "--use-all-modificators"
}

SDL_GENERATOR_BIN = $$PWD/../../Bin/TOOLS_Qt$${QT_MAJOR_VERSION}_$$COMPILER_CODE/$$SDL_GENERATOR_BIN

SDL_GENERATOR_OUT_SUBFOLDER = $$OUT_PWD/../../../AuxInclude/Qt$${QT_MAJOR_VERSION}_$$COMPILER_CODE/GeneratedFiles

SDL_GENERATOR_CONFIG_PATH = $$PWD/../SDL/CommonCXX.cfg
SDL_GENERATOR_QML_CONFIG_PATH = $$PWD/../SDL/CommonQML.cfg

# Paths-fix for windows
win32{
	SDL_SCHEMES_LIST ~= s,/,\\,g
	SDL_GENERATOR_OUTPUT_DIR ~= s,/,\\,g
	SDL_GENERATOR_OUT_SUBFOLDER ~= s,/,\\,g
	SDL_GENERATED_FILES ~= s,/,\\,g
	SDL_GENERATOR_CONFIG_PATH ~= s,/,\\,g
	SDL_GENERATOR_QML_CONFIG_PATH ~= s,/,\\,g
}

################################# CXX #################################

SDL_CXX_GENERATOR_COMMAND_BASE = $$SDL_GENERATOR_BIN $$SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS --generator=QMake -O=$${SDL_GENERATOR_OUT_SUBFOLDER}
win32{
	SDL_CXX_GENERATOR_COMMAND_BASE ~= s,/,\\,g
}

SDL_C_COMPILER.name = SDL_Compiler_IMPL-$${TARGET}
SDL_C_COMPILER.commands = $${SDL_CXX_GENERATOR_COMMAND_BASE} -GS ${QMAKE_FILE_IN} --config=$${SDL_GENERATOR_CONFIG_PATH} --cpp-mode=IMPL
SDL_C_COMPILER.input = SDL_SCHEMES_LIST
SDL_C_COMPILER.output = $${SDL_GENERATOR_OUT_SUBFOLDER}/$${TARGET}/SDL/1.0/CPP/${QMAKE_FILE_BASE}.cpp
SDL_C_COMPILER.dependency_type = TYPE_C
SDL_C_COMPILER.variable_out = SOURCES
SDL_C_COMPILER.CONFIG = target_predeps
SDL_C_COMPILER.depends = ${QMAKE_FILE_IN} $${SDL_GENERATOR_BIN}
QMAKE_EXTRA_COMPILERS += SDL_C_COMPILER

SDL_H_COMPILER.name = SDL_Compiler_Header-$${TARGET}
SDL_H_COMPILER.commands = $${SDL_CXX_GENERATOR_COMMAND_BASE} -GS ${QMAKE_FILE_IN} --config=$${SDL_GENERATOR_CONFIG_PATH} --cpp-mode=HEADER
SDL_H_COMPILER.input = SDL_SCHEMES_LIST
SDL_H_COMPILER.output =  $${SDL_GENERATOR_OUT_SUBFOLDER}/$${TARGET}/SDL/1.0/CPP/${QMAKE_FILE_BASE}.h
SDL_H_COMPILER.dependency_type = TYPE_C
SDL_H_COMPILER.variable_out = HEADERS
SDL_H_COMPILER.CONFIG = target_predeps
SDL_H_COMPILER.depends = ${QMAKE_FILE_IN} $${SDL_GENERATOR_BIN}
QMAKE_EXTRA_COMPILERS += SDL_H_COMPILER

SDL_MOC_COMPILER.name = SDL_MOC_Compiler-$${TARGET}
SDL_MOC_COMPILER.commands = moc $${SDL_GENERATOR_OUT_SUBFOLDER}/$${TARGET}/SDL/1.0/CPP/${QMAKE_FILE_BASE}.h -o $${SDL_GENERATOR_OUT_SUBFOLDER}/$${TARGET}/SDL/1.0/CPP/moc_${QMAKE_FILE_BASE}.cpp
SDL_MOC_COMPILER.input = HEADERS
SDL_MOC_COMPILER.output = $${SDL_GENERATOR_OUT_SUBFOLDER}/$${TARGET}/SDL/1.0/CPP/moc_${QMAKE_FILE_BASE}.cpp
SDL_MOC_COMPILER.dependency_type = TYPE_C
SDL_MOC_COMPILER.variable_out = SOURCES
SDL_MOC_COMPILER.CONFIG = target_predeps
SDL_MOC_COMPILER.depends = ${QMAKE_FILE_IN} $${SDL_GENERATOR_BIN}
QMAKE_EXTRA_COMPILERS += SDL_MOC_COMPILER


SDL_QRC_COMPILER.name = SDL_QRC_Compiler-$${TARGET}
SDL_QRC_COMPILER.commands = rcc $${SDL_GENERATOR_OUT_SUBFOLDER}/$${TARGET}/SDL/1.0/QML/${QMAKE_FILE_BASE}/${QMAKE_FILE_BASE}.qrc -name ${QMAKE_FILE_BASE} -o $${SDL_GENERATOR_OUT_SUBFOLDER}/$${TARGET}/SDL/1.0/CPP/qrc_${QMAKE_FILE_BASE}.cpp
SDL_QRC_COMPILER.input = RESOURCES
SDL_QRC_COMPILER.output = $${SDL_GENERATOR_OUT_SUBFOLDER}/$${TARGET}/SDL/1.0/CPP/qrc_${QMAKE_FILE_BASE}.cpp
SDL_QRC_COMPILER.dependency_type = TYPE_C
SDL_QRC_COMPILER.variable_out = SOURCES
SDL_QRC_COMPILER.CONFIG = target_predeps
SDL_QRC_COMPILER.depends = ${QMAKE_FILE_IN} $${SDL_GENERATOR_BIN}
QMAKE_EXTRA_COMPILERS += SDL_QRC_COMPILER

################################# QML #################################
################################# QML #################################
RCC_EXECUTABLE_PATH = $$[QT_HOST_LIBEXECS]/$$QMAKE_RCC
SDL_QML_GENERATOR_COMMAND_BASE = $$SDL_GENERATOR_BIN $$SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS --generator=QMake -O=$${SDL_GENERATOR_OUT_SUBFOLDER} --config=$${SDL_GENERATOR_QML_CONFIG_PATH} --QML


win32 {
        RCC_EXECUTABLE_PATH = $$[QT_INSTALL_BINS]/$$QMAKE_RCC
        RCC_EXECUTABLE_PATH ~= s,/,\\,g
        SDL_OUTPUT_FILES_QML ~= s,/,\\,g
}


SDL_TARGET = $$replace(TARGET, sdl, '')${QMAKE_FILE_BASE}Sdl
CPP_SDL_OUTPUT_FILE_QML = $${SDL_GENERATOR_OUT_SUBFOLDER}/$${TARGET}/SDL/1.0/QML/$${TARGET}/_qrcWrap/qrc_$${TARGET}Sdl.cpp

SDL_QML_COMPILER.name = SDL-Compiler-QML-$${TARGET}
SDL_QML_COMPILER.commands = $${SDL_QML_GENERATOR_COMMAND_BASE} -GS ${QMAKE_FILE_IN}
SDL_QML_COMPILER.input = SDL_SCHEMES_LIST
SDL_QML_COMPILER.output = $${SDL_GENERATOR_OUT_SUBFOLDER}/$${TARGET}/SDL/1.0/QML/$$replace(TARGET, sdl, '')${QMAKE_FILE_BASE}Sdl/$$replace(TARGET, sdl, '')${QMAKE_FILE_BASE}Sdl.qrc
SDL_QML_COMPILER.dependency_type = TYPE_QRC
SDL_QML_COMPILER.variable_out = RESOURCES
SDL_QML_COMPILER.CONFIG = target_predeps
SDL_QML_COMPILER.depends = ${QMAKE_FILE_IN} $${SDL_GENERATOR_BIN}
QMAKE_EXTRA_COMPILERS += SDL_QML_COMPILER

