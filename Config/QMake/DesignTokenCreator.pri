
#----------------------------------------------------------- The DESIGN TOKEN CREATOR config
# Setting the executable file name for specific OS
COMPILER_DIR = $$CONFIGURATION_NAME$$COMPILER_CODE
win32{
	DESIGN_TOKEN_CREATOR_EXE = DesignTokenCreatorExe.exe
}
else{
	DESIGN_TOKEN_CREATOR_EXE = DesignTokenCreatorExe
}

#  Setting the executable absolutely file path for specific OS
DESIGN_TOKEN_CREATOR_EXE_BIN = $$PWD/../../Bin/$$COMPILER_DIR/$$DESIGN_TOKEN_CREATOR_EXE

win32{
	ARX_COMPILER = Arxc.exe
	ACF_TOOL = Acf.exe
	QMAKE_RCC = rcc.exe
	COPY_FILE = copy
}
else{
	ARX_COMPILER = Arxc
	ACF_TOOL = Acf
	QMAKE_RCC = rcc
	COPY_FILE = cp
}




DESIGN_TOKEN_CREATOR_INPUT_THEME_FILES = $$files($$DESIGN_TOKEN_CREATOR_INPUT_THEME_BASE_PATH/*.theme, false)
LOWER_TARGET = $$lower($$TARGET)

DESIGN_TOKEN_CREATOR_COMMAND_PARAMS = -F $$absolute_path(${QMAKE_FILE_IN_BASE}${QMAKE_FILE_EXT}, $$DESIGN_TOKEN_CREATOR_INPUT_THEME_BASE_PATH) -I $$DESIGN_TOKEN_CREATOR_COMMAND_PARAM_IMAGES_INPUT_DIR -S $$DESIGN_TOKEN_CREATOR_COMMAND_PARAM_STYIES_INPUT_DIR -OD $$DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR -P $$LOWER_TARGET
DESIGN_TOKEN_CREATOR_COMMAND = $$DESIGN_TOKEN_CREATOR_EXE_BIN $$DESIGN_TOKEN_CREATOR_COMMAND_PARAMS -env_vars $$ENV_VARS


GENERATE_RESOURCE_COMMANDS = $$[QT_INSTALL_BINS]/$$QMAKE_RCC -name $$LOWER_TARGET${QMAKE_FILE_BASE} $${DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR}/$$LOWER_TARGET${QMAKE_FILE_BASE}.qrc -o $${DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR}/qrc_$$LOWER_TARGET${QMAKE_FILE_BASE}.cpp
GENERATED_RESOURCE_OUTPUT = $${DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR}/qrc_$$LOWER_TARGET${QMAKE_FILE_BASE}.cpp

# Paths-fix for windows
win32{
	DESIGN_TOKEN_CREATOR_EXE_BIN ~= s,/,\\,g
	DESIGN_TOKEN_CREATOR_COMMAND_PARAM_FILE ~= s,/,\\,g
	DESIGN_TOKEN_CREATOR_COMMAND_PARAM_INPUT_DIR ~= s,/,\\,g
	DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR ~= s,/,\\,g

	GENERATE_RESOURCE_COMMANDS ~= s,/,\\,g
	GENERATED_RESOURCE_OUTPUT ~= s,/,\\,g
	GENERATED_RESOURCES ~= s,/,\\,g
	DESIGN_TOKEN_CREATOR_INPUT_THEME_FILES ~= s,/,\\,g
}

message($$TARGET $$DESIGN_TOKEN_CREATOR_EXE_BIN)

themes$${TARGET}.name = $${TARGET} Design Token Compiler
themes$${TARGET}.commands = $${DESIGN_TOKEN_CREATOR_COMMAND} && $${GENERATE_RESOURCE_COMMANDS}
themes$${TARGET}.input = DESIGN_TOKEN_CREATOR_INPUT_THEME_FILES
themes$${TARGET}.output = $${GENERATED_RESOURCE_OUTPUT}
themes$${TARGET}.dependency_type = TYPE_C
themes$${TARGET}.variable_out = SOURCES
themes$${TARGET}.CONFIG += no_link target_predeps
QMAKE_EXTRA_COMPILERS += themes$${TARGET}

