win32{
	DESIGN_TOKEN_CREATOR_EXE = DesignTokenCreator.exe
}
else{
	DESIGN_TOKEN_CREATOR_EXE = DesignTokenCreator
}

#  Set the executable absolutely file path for specific OS
DESIGN_TOKEN_CREATOR_EXE_BIN = $$PWD/../../Bin/$$COMPILER_DIR/$$DESIGN_TOKEN_CREATOR_EXE

win32{
	QMAKE_RCC = rcc.exe
	COPY_FILE = copy
}
else{
	QMAKE_RCC = rcc
	COPY_FILE = cp
}

THEME_FILES = $$files($$DESIGN_TOKEN_CREATOR_INPUT_THEME_BASE_PATH/*.theme, false)
LOWER_TARGET = $$lower($$TARGET)

DESIGN_TOKEN_CREATOR_COMMAND_PARAMS = -F $$absolute_path(${QMAKE_FILE_IN_BASE}${QMAKE_FILE_EXT}, $$DESIGN_TOKEN_CREATOR_INPUT_THEME_BASE_PATH) -I $$DESIGN_TOKEN_CREATOR_COMMAND_PARAM_IMAGES_INPUT_DIR -S $$DESIGN_TOKEN_CREATOR_COMMAND_PARAM_STYIES_INPUT_DIR -OD $$DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR -P $$LOWER_TARGET
DESIGN_TOKEN_CREATOR_COMMAND = $$DESIGN_TOKEN_CREATOR_EXE_BIN $$DESIGN_TOKEN_CREATOR_COMMAND_PARAMS

GENERATE_RESOURCE_COMMANDS = $$[QT_INSTALL_BINS]/$$QMAKE_RCC -name $$LOWER_TARGET${QMAKE_FILE_BASE} $${DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR}/$$LOWER_TARGET${QMAKE_FILE_BASE}.qrc -o $${DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR}/qrc_$$LOWER_TARGET${QMAKE_FILE_BASE}.cpp
GENERATED_RESOURCE_OUTPUT = $${DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR}/qrc_$$LOWER_TARGET${QMAKE_FILE_BASE}.cpp

GENERATED_WEB_RESOURCE_OUTPUT = $${DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR}/qrc_Web$${LOWER_TARGET}.cpp
QRC_WEB_CURRENT_FILE = $${DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR}/Web$${LOWER_TARGET}.qrc

GENERATE_WEB_RESOURCE_COMMANDS = $$[QT_INSTALL_BINS]/$$QMAKE_RCC -name Web$${LOWER_TARGET} $${QRC_WEB_CURRENT_FILE} -o $${GENERATED_WEB_RESOURCE_OUTPUT}

# Paths-fix for windows
win32{
	DESIGN_TOKEN_CREATOR_EXE_BIN ~= s,/,\\,g
	DESIGN_TOKEN_CREATOR_COMMAND ~= s,/,\\,g
	DESIGN_TOKEN_CREATOR_COMMAND_PARAM_FILE ~= s,/,\\,g
	DESIGN_TOKEN_CREATOR_COMMAND_PARAM_INPUT_DIR ~= s,/,\\,g
	DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR ~= s,/,\\,g

	GENERATED_RESOURCE_OUTPUT ~= s,/,\\,g

    GENERATED_WEB_RESOURCE_OUTPUT ~= s,/,\\,g
	QRC_WEB_CURRENT_FILE ~= s,/,\\,g
	GENERATE_WEB_RESOURCE_COMMANDS ~= s,/,\\,g

    THEME_FILES ~= s,/,\\,g
}

themes$${TARGET}.name = Theme Files
themes$${TARGET}.commands = $${DESIGN_TOKEN_CREATOR_COMMAND} -W --ignore-errors && $${GENERATE_RESOURCE_COMMANDS} && $${GENERATE_WEB_RESOURCE_COMMANDS}
themes$${TARGET}.input = THEME_FILES
themes$${TARGET}.output = $${GENERATED_RESOURCE_OUTPUT}
#themes$${TARGET}.depend_command = $${GENERATE_WEB_RESOURCE_COMMANDS}
themes$${TARGET}.dependency_type = TYPE_C
themes$${TARGET}.variable_out = SOURCES
themes$${TARGET}.CONFIG += no_link target_predeps
QMAKE_EXTRA_COMPILERS += themes$${TARGET}

web$${TARGET}.target = $${GENERATED_WEB_RESOURCE_OUTPUT}
web$${TARGET}.commands = echo $$dummyfile.target
QMAKE_EXTRA_TARGETS += web$${TARGET}
GENERATED_SOURCES += $${GENERATED_WEB_RESOURCE_OUTPUT}

