win32{
	QMAKE_RCC = rcc.exe
	COPY_FILE = copy
	SDL_GENERATOR_BIN = SdlCodeGenerator.exe
}
else{
	QMAKE_RCC = rcc
	COPY_FILE = cp
	SDL_GENERATOR_BIN = SdlCodeGenerator
}
macx*{
	SDL_GENERATOR_BIN = SdlCodeGenerator.app/Contents/MacOS/SdlCodeGenerator
}

isEmpty(SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS) {
	SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS = "--use-all-modificators"
}

SDL_GENERATOR_BIN = $$PWD/../../Bin/$$COMPILER_DIR/$$SDL_GENERATOR_BIN

SDL_GENERATOR_OUT_SUBFOLDER = $$OUT_PWD/$$AUXINCLUDEPATH/GeneratedFiles/$$TARGET/SDL



############### -for- loop
for(SDL_GENERATOR_SCHEME_PATH, SDL_SOURCES){

	SDL_GENERATOR_COMMAND_BASE = $$SDL_GENERATOR_BIN -S $$SDL_GENERATOR_SCHEME_PATH $$SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS --auto-join --generator=QMake  -O=$${SDL_GENERATOR_OUT_SUBFOLDER}
	SDL_GENERATED_FILES = $$system($${SDL_GENERATOR_COMMAND_BASE} -D --CPP --GQL)

	SDL_GENERATOR_QML_MODULE_NAME = $$last($$list($$split(SDL_OUTPUT_FILES_QML, /)))
	SDL_GENERATOR_QML_MODULE_NAME = $$first($$list($$split(SDL_GENERATOR_QML_MODULE_NAME, .)))

	# Paths-fix for windows
	win32{
		SDL_GENERATOR_COMMAND_BASE ~= s,/,\\,g
		SDL_GENERATOR_SCHEME_PATH ~= s,/,\\,g
		SDL_GENERATOR_OUTPUT_DIR ~= s,/,\\,g
		SDL_GENERATOR_OUT_SUBFOLDER ~= s,/,\\,g
		SDL_GENERATED_FILES ~= s,/,\\,g
	}

	message ("processing SDL '$$SDL_GENERATOR_SCHEME_PATH' of $$TARGET ")

	#CPP
	# custom build for CPP
	cppSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.name = SDL-Compiler-CPP
	cppSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.CONFIG += no_link
	cppSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.CONFIG += target_predeps
	cppSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.output = $${SDL_GENERATED_FILES}
	cppSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.commands = $${SDL_GENERATOR_COMMAND_BASE} -G --CPP --GQL
	cppSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.input = SDL_GENERATOR_SCHEME_PATH
	cppSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.variable_out = SOURCES
	cppSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.dependency_type = TYPE_C
	cppSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.depends = $$SDL_GENERATOR_SCHEME_PATH
	QMAKE_EXTRA_COMPILERS += cppSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}
	GENERATED_SOURCES += $${SDL_GENERATED_FILES}


	#QML
	RCC_EXECUTABLE_PATH = $$[QT_HOST_LIBEXECS]/$$QMAKE_RCC

	# RCC path for windows
	win32 {
		RCC_EXECUTABLE_PATH = $$[QT_INSTALL_BINS]/$$QMAKE_RCC
	}

	# output files
	SDL_OUTPUT_FILES_QML = $$system($${SDL_GENERATOR_COMMAND_BASE} -D --QML)

	CPP_SDL_OUTPUT_FILES_QML = $${SDL_GENERATOR_QML_OUT_SUBFOLDER}/_qrcWrap/qrc_$${SDL_GENERATOR_SCHEME_NAME}.cpp

	GENERATE_RESOURCE_COMMANDS = $${RCC_EXECUTABLE_PATH} -name $${SDL_GENERATOR_QML_MODULE_NAME} $${SDL_OUTPUT_FILES_QML} -o $${CPP_SDL_OUTPUT_FILES_QML}

	# custom build for QML
	qmlSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.name = SDL-Compiler-QML
	qmlSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.CONFIG += no_link
	qmlSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.CONFIG += target_predeps
	qmlSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.output = $$CPP_SDL_OUTPUT_FILES_QML
	qmlSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.commands = $$SDL_GENERATOR_COMMAND_BASE -G --QML && $${GENERATE_RESOURCE_COMMANDS}
	qmlSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.input = SDL_GENERATOR_SCHEME_PATH
	qmlSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.variable_out = SOURCES
	qmlSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.dependency_type = TYPE_C
	qmlSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}.depends = $$SDL_GENERATOR_SCHEME_PATH
	QMAKE_EXTRA_COMPILERS += qmlSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}

}
