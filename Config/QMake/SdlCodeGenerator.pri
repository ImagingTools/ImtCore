win32{
	QMAKE_RCC = rcc.exe
	COPY_FILE = copy
	SDL_GENERATOR_BIN = SdlCodeGenerator.exe
}
else{
	QMAKE_RCC = rcc
	COPY_FILE = cp
	SDL_GENERATOR_BIN = SdlCodeGenerator
}
macx*{
	SDL_GENERATOR_BIN = SdlCodeGenerator.app/Contents/MacOS/SdlCodeGenerator
}

isEmpty(SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS) {
	SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS = "--use-all-modificators"
}

SDL_GENERATOR_BIN = $$PWD/../../Bin/TOOLS_Qt$${QT_MAJOR_VERSION}_$$COMPILER_CODE/$$SDL_GENERATOR_BIN

SDL_GENERATOR_OUT_SUBFOLDER = $$OUT_PWD/../../../AuxInclude/Qt$${QT_MAJOR_VERSION}_$$COMPILER_CODE/GeneratedFiles

SDL_GENERATOR_CONFIG_PATH = $$PWD/../SDL/CommonCXX.cfg
SDL_GENERATOR_QML_CONFIG_PATH = $$PWD/../SDL/CommonQML.cfg

# Paths-fix for windows
win32{
	SDL_GENERATOR_COMMAND_BASE ~= s,/,\\,g
	SDL_GENERATOR_SCHEME_PATH ~= s,/,\\,g
	SDL_GENERATOR_OUTPUT_DIR ~= s,/,\\,g
	SDL_GENERATOR_OUT_SUBFOLDER ~= s,/,\\,g
	SDL_GENERATED_FILES ~= s,/,\\,g
	SDL_GENERATOR_CONFIG_PATH ~= s,/,\\,g
	SDL_GENERATOR_QML_CONFIG_PATH ~= s,/,\\,g
}


#! warning you MUST specify {SDL_GENERATOR_SCHEME_PATH} variable BEFORE call this function!!!! QMAKE_EXTRA_COMPILERS limitation!!!!
defineReplace(CreateSdlTarget) {
	SDL_GENERATOR_QML_MODULE_NAME = $$1
	SDL_GENERATOR_SCHEME_PATH = $$2

	SDL_GENERATOR_COMMAND_BASE = $$SDL_GENERATOR_BIN $$SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS --generator=QMake -O=$${SDL_GENERATOR_OUT_SUBFOLDER}
	win32{
		SDL_GENERATOR_COMMAND_BASE ~= s,/,\\,g
	}

	SDL_GENERATED_FILES = $$system($${SDL_GENERATOR_COMMAND_BASE} -DS $${SDL_GENERATOR_SCHEME_PATH} --config=$${SDL_GENERATOR_CONFIG_PATH})

	SDL_COMPILER_NAME = cppSdlCompile$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}
	$${SDL_COMPILER_NAME}.name = SDL_Compiler_CPP-$${SDL_GENERATOR_QML_MODULE_NAME}
	$${SDL_COMPILER_NAME}.target = $$first($$list($${SDL_GENERATED_FILES}))
	$${SDL_COMPILER_NAME}.commands = $${SDL_GENERATOR_COMMAND_BASE} -GS $${SDL_GENERATOR_SCHEME_PATH} --config=$${SDL_GENERATOR_CONFIG_PATH}
	$${SDL_COMPILER_NAME}.input = SDL_GENERATOR_SCHEME_PATH
	$${SDL_COMPILER_NAME}.output = $${SDL_GENERATED_FILES}
	$${SDL_COMPILER_NAME}.dependency_type = TYPE_C
	$${SDL_COMPILER_NAME}.variable_out = SOURCES
	$${SDL_COMPILER_NAME}.CONFIG = target_predeps
	$${SDL_COMPILER_NAME}.depends = $${SDL_GENERATOR_SCHEME_PATH} $${SDL_GENERATOR_BIN}

	export($${SDL_COMPILER_NAME}.name)
	export($${SDL_COMPILER_NAME}.target)
	export($${SDL_COMPILER_NAME}.commands)
	export($${SDL_COMPILER_NAME}.input)
	export($${SDL_COMPILER_NAME}.output)
	export($${SDL_COMPILER_NAME}.dependency_type)
	export($${SDL_COMPILER_NAME}.variable_out)
	export($${SDL_COMPILER_NAME}.CONFIG)
	export($${SDL_COMPILER_NAME}.depends)

	OTHER_FILES += $${SDL_GENERATOR_SCHEME_PATH}
	export (OTHER_FILES)

	return ($${SDL_COMPILER_NAME})
}


#QML
RCC_EXECUTABLE_PATH = $$[QT_HOST_LIBEXECS]/$$QMAKE_RCC

# RCC path for windows
win32 {
	RCC_EXECUTABLE_PATH = $$[QT_INSTALL_BINS]/$$QMAKE_RCC
}


#! warning you MUST specify {SDL_GENERATOR_SCHEME_PATH} variable BEFORE call this function!!!! QMAKE_EXTRA_COMPILERS limitation!!!!
defineReplace(CreateQmlSdlTarget) {
	SDL_GENERATOR_QML_MODULE_NAME = $$1
	SDL_GENERATOR_SCHEME_PATH = $$2

	SDL_GENERATOR_COMMAND_BASE = $$SDL_GENERATOR_BIN $$SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS --generator=QMake -O=$${SDL_GENERATOR_OUT_SUBFOLDER} --config=$${SDL_GENERATOR_QML_CONFIG_PATH}
	win32{
		SDL_GENERATOR_COMMAND_BASE ~= s,/,\\,g
	}

	SDL_OUTPUT_FILES_QML += $$system($${SDL_GENERATOR_COMMAND_BASE} -DS $${SDL_GENERATOR_SCHEME_PATH} --QML)

	DIR_SEPARATOR = /
	win32{
		SDL_OUTPUT_FILES_QML ~= s,/,\\,g
		DIR_SEPARATOR = '\\'
	}

	QRC_WRAP_DIR_PATH = $$split(SDL_OUTPUT_FILES_QML, $${DIR_SEPARATOR})

	isEmpty(QRC_WRAP_DIR_PATH){
		message("QRC_WRAP_DIR_PATH IS EMPTY variable is empty. SDL output files were not defined.")
	}

	QRC_NAME = $$take_last(QRC_WRAP_DIR_PATH)
	QRC_NAME = $$split(QRC_NAME, .)
	QRC_NAME = $$first(QRC_NAME)
	QRC_WRAP_DIR_PATH = $$join(QRC_WRAP_DIR_PATH, $${DIR_SEPARATOR})

	# add first slash
	!win32 {
		QRC_WRAP_DIR_PATH = /$${QRC_WRAP_DIR_PATH}
	}

	CPP_SDL_OUTPUT_FILES_QML = $${QRC_WRAP_DIR_PATH}/qrc_$${QRC_NAME}.cpp
	# Paths-fix for windows
	win32{
		CPP_SDL_OUTPUT_FILES_QML ~= s,/,\\,g
	}

	GENERATE_RESOURCE_COMMANDS = $${RCC_EXECUTABLE_PATH} -name $${QRC_NAME} $${SDL_OUTPUT_FILES_QML} -o $${CPP_SDL_OUTPUT_FILES_QML}

	QML_SDL_COMPILER_NAME = qmlSdlCompiler$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}
	$${QML_SDL_COMPILER_NAME}.name = SDL-Compiler-QML-$${SDL_GENERATOR_QML_MODULE_NAME}
	$${QML_SDL_COMPILER_NAME}.target = $${CPP_SDL_OUTPUT_FILES_QML}
	$${QML_SDL_COMPILER_NAME}.commands = $${SDL_GENERATOR_COMMAND_BASE} -GS $${SDL_GENERATOR_SCHEME_PATH} --QML && $${GENERATE_RESOURCE_COMMANDS}
	$${QML_SDL_COMPILER_NAME}.input = SDL_GENERATOR_SCHEME_PATH
	$${QML_SDL_COMPILER_NAME}.output = $${CPP_SDL_OUTPUT_FILES_QML}
	$${QML_SDL_COMPILER_NAME}.dependency_type = TYPE_C
	$${QML_SDL_COMPILER_NAME}.variable_out = SOURCES
	$${QML_SDL_COMPILER_NAME}.CONFIG = target_predeps
	$${QML_SDL_COMPILER_NAME}.depends = $${SDL_GENERATOR_SCHEME_PATH} $${SDL_GENERATOR_BIN}

	export($${QML_SDL_COMPILER_NAME}.name)
	export($${QML_SDL_COMPILER_NAME}.target)
	export($${QML_SDL_COMPILER_NAME}.commands)
	export($${QML_SDL_COMPILER_NAME}.input)
	export($${QML_SDL_COMPILER_NAME}.output)
	export($${QML_SDL_COMPILER_NAME}.dependency_type)
	export($${QML_SDL_COMPILER_NAME}.variable_out)
	export($${QML_SDL_COMPILER_NAME}.CONFIG)
	export($${QML_SDL_COMPILER_NAME}.depends)

	# additional target
	QML_SDL_TARGET_NAME = qmlSdlTarget$${TARGET}$${SDL_GENERATOR_QML_MODULE_NAME}
	$${QML_SDL_TARGET_NAME}.target = $${CPP_SDL_OUTPUT_FILES_QML}
	$${QML_SDL_TARGET_NAME}.commands = $${SDL_GENERATOR_COMMAND_BASE} -GS $${SDL_GENERATOR_SCHEME_PATH} --QML && $${GENERATE_RESOURCE_COMMANDS}
	QMAKE_EXTRA_TARGETS += $${QML_SDL_TARGET_NAME}

	export($${QML_SDL_TARGET_NAME})
	export($${QML_SDL_TARGET_NAME}.target)
	export($${QML_SDL_TARGET_NAME}.commands)
	export(QMAKE_EXTRA_TARGETS)

	# Add generated cource
	GENERATED_SOURCES += $${CPP_SDL_OUTPUT_FILES_QML}
	export(GENERATED_SOURCES)

	return ($${QML_SDL_COMPILER_NAME})
}


defineReplace(CreateCppQmlSdlTarget) {

	SDL_GENERATOR_MODULE_NAME = $$1
	SDL_GENERATOR_SCHEME_PATH = $$2

	SDL_CPP_QML_COMPILER = $$CreateSdlTarget($$1, $$2)
	SDL_QML_COMPILER = $$CreateQmlSdlTarget($$1, $$2)

	$${SDL_CPP_QML_COMPILER}.name = SDL-$${SDL_GENERATOR_MODULE_NAME}-Compiler
	$${SDL_CPP_QML_COMPILER}.target = $${SDL_GENERATOR_SCHEME_PATH}
	$${SDL_CPP_QML_COMPILER}.commands += && $$eval($${SDL_QML_COMPILER}.commands)
	$${SDL_CPP_QML_COMPILER}.output += $$eval($${SDL_QML_COMPILER}.output)

	export($${SDL_CPP_QML_COMPILER}.name)
	export($${SDL_CPP_QML_COMPILER}.target)
	export($${SDL_CPP_QML_COMPILER}.commands)
	export($${SDL_CPP_QML_COMPILER}.input)
	export($${SDL_CPP_QML_COMPILER}.output)
	export($${SDL_CPP_QML_COMPILER}.dependency_type)
	export($${SDL_CPP_QML_COMPILER}.variable_out)
	export($${SDL_CPP_QML_COMPILER}.CONFIG)
	export($${SDL_CPP_QML_COMPILER}.depends)

	return ($${SDL_CPP_QML_COMPILER})
}
