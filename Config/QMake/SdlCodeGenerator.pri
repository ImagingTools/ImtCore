win32{
	QMAKE_RCC = rcc.exe
	COPY_FILE = copy
	SDL_GENERATOR_BIN = SdlCodeGenerator.exe
}
else{
	QMAKE_RCC = rcc
	COPY_FILE = cp
	SDL_GENERATOR_BIN = SdlCodeGenerator
}
macx*{
	SDL_GENERATOR_BIN = SdlCodeGenerator.app/Contents/MacOS/SdlCodeGenerator
}

isEmpty(SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS) {
	SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS = "--use-all-modificators"
}

SDL_GENERATOR_BIN = $$PWD/../../Bin/$$COMPILER_DIR/$$SDL_GENERATOR_BIN

SDL_HEADER_JOINED_FILE_PATH = $${SDL_GENERATOR_CPP_OUT_SUBFOLDER}/$${SDL_GENERATOR_SCHEME_NAME}.h
SDL_SOURCE_JOINED_FILE_PATH = $${SDL_GENERATOR_CPP_OUT_SUBFOLDER}/$${SDL_GENERATOR_SCHEME_NAME}.cpp

SDL_GENERATOR_COMMAND_BASE = $$SDL_GENERATOR_BIN -S $$SDL_GENERATOR_SCHEME_PATH $$SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS -JH=$${SDL_HEADER_JOINED_FILE_PATH} -JCPP=$${SDL_SOURCE_JOINED_FILE_PATH}

# Paths-fix for windows
win32{
	SDL_GENERATOR_COMMAND_BASE ~= s,/,\\,g
	SDL_GENERATOR_SCHEME_PATH ~= s,/,\\,g
	SDL_GENERATOR_OUTPUT_DIR ~= s,/,\\,g
	SDL_GENERATOR_CPP_OUT_SUBFOLDER ~= s,/,\\,g
	SDL_GENERATOR_CPP_NAMESPACE ~= s,/,\\,g
	SDL_GENERATOR_QML_OUT_SUBFOLDER ~= s,/,\\,g
	SDL_GENERATOR_QML_MODULE_NAME ~= s,/,\\,g
}

message ("---------SDL Generator config")
message ("SDL_GENERATOR_SCHEME_PATH = '$$SDL_GENERATOR_SCHEME_PATH'")
message ("SDL_GENERATOR_CPP_OUT_SUBFOLDER = '$$SDL_GENERATOR_CPP_OUT_SUBFOLDER'")
message ("SDL_GENERATOR_CPP_NAMESPACE = '$$SDL_GENERATOR_CPP_NAMESPACE'")
message ("SDL_GENERATOR_QML_OUT_SUBFOLDER = '$$SDL_GENERATOR_QML_OUT_SUBFOLDER'")
message ("SDL_GENERATOR_QML_MODULE_NAME = '$$SDL_GENERATOR_QML_MODULE_NAME'")


#CPP
message ("SDL_GENERATOR_CPP_OUT '$${SDL_HEADER_JOINED_FILE_PATH} $${SDL_SOURCE_JOINED_FILE_PATH}'")

# custom build for CPP
cppSdlCompiler$${TARGET}.name = SDL-Compiler-CPP
cppSdlCompiler$${TARGET}.CONFIG += no_link
cppSdlCompiler$${TARGET}.CONFIG += target_predeps
cppSdlCompiler$${TARGET}.output = $${SDL_HEADER_JOINED_FILE_PATH} $${SDL_SOURCE_JOINED_FILE_PATH}
cppSdlCompiler$${TARGET}.commands = $${SDL_GENERATOR_COMMAND_BASE} -G --CPP --GQL -N=$${SDL_GENERATOR_CPP_NAMESPACE} -O=$${SDL_GENERATOR_CPP_OUT_SUBFOLDER}
cppSdlCompiler$${TARGET}.input = SDL_GENERATOR_SCHEME_PATH
cppSdlCompiler$${TARGET}.variable_out = SOURCES
cppSdlCompiler$${TARGET}.dependency_type = TYPE_C
cppSdlCompiler$${TARGET}.depends = $$SDL_GENERATOR_SCHEME_PATH
QMAKE_EXTRA_COMPILERS += cppSdlCompiler$${TARGET}


#QML

# output files
SDL_OUTPUT_FILES_QML = $${SDL_GENERATOR_QML_OUT_SUBFOLDER}/$${SDL_GENERATOR_QML_MODULE_NAME}.qrc
CPP_SDL_OUTPUT_FILES_QML = ${SDL_GENERATOR_QML_OUT_SUBFOLDER}/_qrcWrap/qrc_$${SDL_GENERATOR_SCHEME_NAME}.cpp
message ("SDL_OUTPUT_FILES_QML '$${SDL_OUTPUT_FILES_QML}'")

RCC_EXECUTABLE_PATH = $$[QT_HOST_LIBEXECS]/$$QMAKE_RCC

# RCC path for windows
win32 {
	RCC_EXECUTABLE_PATH = $$[QT_INSTALL_BINS]/$$QMAKE_RCC
}

GENERATE_RESOURCE_COMMANDS = $${RCC_EXECUTABLE_PATH} -name $${SDL_GENERATOR_QML_MODULE_NAME} $${SDL_OUTPUT_FILES_QML} -o $${CPP_SDL_OUTPUT_FILES_QML}


# custom build for QML
qmlSdlCompiler$${TARGET}.name = SDL-Compiler-QML
qmlSdlCompiler$${TARGET}.CONFIG += no_link
qmlSdlCompiler$${TARGET}.CONFIG += target_predeps
qmlSdlCompiler$${TARGET}.output = $$SDL_OUTPUT_FILES_QML
qmlSdlCompiler$${TARGET}.commands = $$SDL_GENERATOR_COMMAND_BASE -G --QML -N=$${SDL_GENERATOR_QML_MODULE_NAME} -O=$${SDL_GENERATOR_QML_OUT_SUBFOLDER} && $${GENERATE_RESOURCE_COMMANDS} && $${GENERATE_THEME_RESOURCE_COMMANDS}
qmlSdlCompiler$${TARGET}.input = SDL_GENERATOR_SCHEME_PATH
qmlSdlCompiler$${TARGET}.variable_out = SOURCES
qmlSdlCompiler$${TARGET}.dependency_type = TYPE_C
qmlSdlCompiler$${TARGET}.depends = $$SDL_GENERATOR_SCHEME_PATH
QMAKE_EXTRA_COMPILERS += qmlSdlCompiler$${TARGET}
