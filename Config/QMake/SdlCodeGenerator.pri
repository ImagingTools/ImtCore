win32{
	QMAKE_RCC = rcc.exe
	COPY_FILE = copy
	SDL_GENERATOR_BIN = SdlCodeGenerator.exe
}
else{
	QMAKE_RCC = rcc
	COPY_FILE = cp
	SDL_GENERATOR_BIN = SdlCodeGenerator
}

isEmpty(SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS) {
	SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS = "--use-all-modificators"
}

SDL_GENERATOR_BIN = $$PWD/../../Bin/$$COMPILER_DIR/$$SDL_GENERATOR_BIN
SDL_GENERATOR_COMMAND_BASE = $$SDL_GENERATOR_BIN -S $$SDL_GENERATOR_SCHEME_PATH $$SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS

# Paths-fix for windows
win32{
	SDL_GENERATOR_COMMAND_BASE ~= s,/,\\,g
	SDL_GENERATOR_SCHEME_PATH ~= s,/,\\,g
	SDL_GENERATOR_OUTPUT_DIR ~= s,/,\\,g
	SDL_GENERATOR_CPP_OUT_SUBFOLDER ~= s,/,\\,g
	SDL_GENERATOR_CPP_NAMESPACE ~= s,/,\\,g
	SDL_GENERATOR_QML_OUT_SUBFOLDER ~= s,/,\\,g
	SDL_GENERATOR_QML_MODULE_NAME ~= s,/,\\,g
}

message ("---------SDL Generator start")
message ("SDL_GENERATOR_SCHEME_PATH = '$$SDL_GENERATOR_SCHEME_PATH'")
message ("SDL_GENERATOR_CPP_OUT_SUBFOLDER = '$$SDL_GENERATOR_CPP_OUT_SUBFOLDER'")
message ("SDL_GENERATOR_CPP_NAMESPACE = '$$SDL_GENERATOR_CPP_NAMESPACE'")
message ("SDL_GENERATOR_QML_OUT_SUBFOLDER = '$$SDL_GENERATOR_QML_OUT_SUBFOLDER'")
message ("SDL_GENERATOR_QML_MODULE_NAME = '$$SDL_GENERATOR_QML_MODULE_NAME'")


#CPP

# ask sdl generator for output files
SDL_OUTPUT_FILES_CPP = $$system($$SDL_GENERATOR_COMMAND_BASE -D --CPP --GQL -N=$${SDL_GENERATOR_CPP_NAMESPACE} -O=$${SDL_GENERATOR_CPP_OUT_SUBFOLDER})
SDL_OUTPUT_FILES_CPP_OUT = $$system($$SDL_GENERATOR_COMMAND_BASE -G --CPP --GQL -N=$${SDL_GENERATOR_CPP_NAMESPACE} -O=$${SDL_GENERATOR_CPP_OUT_SUBFOLDER})

SDL_OUTPUT_FILES_QML_QRC = $${SDL_GENERATOR_QML_OUT_SUBFOLDER}/$${SDL_GENERATOR_QML_MODULE_NAME}.qrc
SDL_OUTPUT_FILES_QML_QRC_CPP = $${SDL_GENERATOR_QML_OUT_SUBFOLDER}/qrc_$${SDL_GENERATOR_QML_MODULE_NAME}.cpp

GENERATE_RESOURCE_COMMANDS = $$[QT_INSTALL_BINS]/$$QMAKE_RCC -name $${SDL_GENERATOR_QML_MODULE_NAME} $${SDL_OUTPUT_FILES_QML_QRC} -o $${SDL_OUTPUT_FILES_QML_QRC_CPP}


win32{
SDL_OUTPUT_FILES_CPP ~= s,/,\\,g
SDL_OUTPUT_FILES_CPP_OUT ~= s,/,\\,g
SDL_OUTPUT_FILES_QML_QRC ~= s,/,\\,g
GENERATE_RESOURCE_COMMANDS ~= s,/,\\,g
SDL_OUTPUT_FILES_QML_QRC_CPP ~= s,/,\\,g
}


GENERATED_SOURCES += $${SDL_OUTPUT_FILES_CPP}


#QML

# ask sdl generator for output files
SDL_OUTPUT_FILES_QML = $$system($$SDL_GENERATOR_COMMAND_BASE -G --QML -N=$${SDL_GENERATOR_QML_MODULE_NAME} -O=$${SDL_GENERATOR_QML_OUT_SUBFOLDER}) && $$system($${GENERATE_RESOURCE_COMMANDS})

GENERATED_SOURCES += $$SDL_OUTPUT_FILES_QML_QRC_CPP

