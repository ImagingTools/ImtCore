win32{
	DDL_CODE_CREATOR_EXE = DdlCodeCreator.exe
}
else{
	DDL_CODE_CREATOR_EXE = DdlCodeCreator
}
macx*{
	DDL_CODE_CREATOR_EXE = DdlCodeCreator.app/Contents/MacOS/DdlCodeCreator
}

#  Set the executable absolutely file path for specific OS
DDL_CODE_CREATOR_EXE_BIN = $$PWD/../../Bin/TOOLS_Qt$${QT_MAJOR_VERSION}_$$COMPILER_CODE/$$DDL_CODE_CREATOR_EXE

win32{
	QMAKE_RCC = rcc.exe
	COPY_FILE = copy
}
else{
	QMAKE_RCC = rcc
	COPY_FILE = cp
}

RCC_EXECUTABLE_PATH = $$[QT_HOST_LIBEXECS]/$$QMAKE_RCC

# RCC path for windows
win32 {
	RCC_EXECUTABLE_PATH = $$[QT_INSTALL_BINS]/$$QMAKE_RCC
}


!isEmpty(DDL_OUTPUT_DIR) {
	DDL_CPP_OUTPUT_DIR = $$DDL_OUTPUT_DIR/Cpp
	DDL_QML_OUTPUT_DIR = $$DDL_OUTPUT_DIR/Qml/$$MODULE_QML_NAME
}


#message(DDL_CODE_CREATOR_EXE_BIN $${DDL_CODE_CREATOR_EXE_BIN})

TEMPLATE_FILES = $$files($$DDL_TEMPLATE_INPUT_DIR/*.json, false)
INPUT_FIRST_FILE = $$first(TEMPLATE_FILES)
# message(INPUT_FIRST_FILE $${INPUT_FIRST_FILE})

DDL_CODE_CREATOR_COMMAND_PARAMS = -TD $$DDL_TEMPLATE_INPUT_DIR -CD $$DDL_CPP_OUTPUT_DIR -QD $$DDL_QML_OUTPUT_DIR -MODC $$MODULE_CPP_NAME -MODQ $$MODULE_QML_NAME
DDL_CODE_CREATOR_COMMAND = $$DDL_CODE_CREATOR_EXE_BIN $$DDL_CODE_CREATOR_COMMAND_PARAMS

GENERATE_RESOURCE_COMMANDS = $$RCC_EXECUTABLE_PATH -name $${MODULE_QML_NAME} $${DDL_QML_OUTPUT_DIR}/../$${MODULE_QML_NAME}.qrc -o $${DDL_QML_OUTPUT_DIR}/../qrc_$${MODULE_QML_NAME}.cpp
GENERATED_RESOURCE_OUTPUT = $${DDL_QML_OUTPUT_DIR}/../qrc_$${MODULE_QML_NAME}.cpp

# Paths-fix for windows
win32{
	DESIGN_TOKEN_CREATOR_EXE_BIN ~= s,/,\\,g
	DDL_CODE_CREATOR_COMMAND ~= s,/,\\,g
	DDL_CODE_CREATOR_COMMAND_PARAM_FILE ~= s,/,\\,g
	DDL_CODE_CREATOR_COMMAND_PARAM_INPUT_DIR ~= s,/,\\,g
	DDL_QML_OUTPUT_DIR ~= s,/,\\,g

	GENERATED_RESOURCE_OUTPUT ~= s,/,\\,g

TEMPLATE_FILES ~= s,/,\\,g
}

# DDL_OUTPUT_FILES_CPP = $$system($${DDL_CODE_CREATOR_COMMAND})

system($${DDL_CODE_CREATOR_COMMAND} && $${GENERATE_RESOURCE_COMMANDS})

DDL_OUTPUT_FILES_CPP = $$files($$DDL_CPP_OUTPUT_DIR/*, false)

ddl$${TARGET}.name = DDL Files
ddl$${TARGET}.commands = $${DDL_CODE_CREATOR_COMMAND} && $${GENERATE_RESOURCE_COMMANDS}
ddl$${TARGET}.input = INPUT_FIRST_FILE
ddl$${TARGET}.output = $${GENERATED_RESOURCE_OUTPUT}
ddl$${TARGET}.dependency_type = TYPE_C
ddl$${TARGET}.variable_out = SOURCES
ddl$${TARGET}.CONFIG += no_link target_predeps
QMAKE_EXTRA_COMPILERS += ddl$${TARGET}

#message(GENERATE_RESOURCE_COMMANDS $$first(TEMPLATE_FILES))
#message(GENERATED_RESOURCE_OUTPUT $${GENERATED_RESOURCE_OUTPUT})
#message(DDL_OUTPUT_FILES_CPP $${DDL_OUTPUT_FILES_CPP})

# web$${TARGET}.target = $${GENERATED_QML_RESOURCE_OUTPUT}
# web$${TARGET}.commands = echo $$dummyfile.target
# QMAKE_EXTRA_TARGETS += web$${TARGET}
GENERATED_SOURCES += $${DDL_OUTPUT_FILES_CPP}

