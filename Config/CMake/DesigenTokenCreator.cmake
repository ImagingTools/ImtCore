
include(${ACFDIR}/Config/CMake/GeneralConfig.cmake)

#----------------------------------------------------------- The DESIGN TOKEN CREATOR config
# Setting the executable file name for specific OS
set(COMPILER_DIR ${CMAKE_BUILD_TYPE}${TARGETNAME})
if (win32)
	set(DESIGN_TOKEN_CREATOR_EXE "DesignTokenCreatorExe.exe")
else()
	set(DESIGN_TOKEN_CREATOR_EXE "DesignTokenCreatorExe")
endif()


#  Setting the executable absolutely file path for specific OS
set(DESIGN_TOKEN_CREATOR_EXE_BIN "${CMAKE_CURRENT_SOURCE_DIR}/../../../Bin/${COMPILER_DIR}/${DESIGN_TOKEN_CREATOR_EXE}")

set(DESIGN_TOKEN_CREATOR_COMMAND_PARAMS
	-I
#	-F
#		${DESIGN_TOKEN_CREATOR_COMMAND_PARAM_FILE}
	-ID
		${DESIGN_TOKEN_CREATOR_COMMAND_PARAM_INPUT_DIR}
	-OD
		${DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR}
	-P
		${PROJECT_NAME}
	)

set(QRC_QRC_FILES)

foreach(_file ${DESIGN_TOKEN_CREATOR_INPUT_THEME_FILES})

	string(REPLACE "/" ";" CURRENT_FILE_PARTS ${_file})
	list(GET CURRENT_FILE_PARTS -1 CURRENT_FILE_NAME_WITH_EXT)
	string(REPLACE "." ";" CURRENT_FILE_NAME_WITH_NO_EXT ${CURRENT_FILE_NAME_WITH_EXT})
	list(GET CURRENT_FILE_NAME_WITH_NO_EXT 0 CURRENT_FILE_NAME)

	set(QRC_QRC_CURRENT_FILE ${DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR}/${CURRENT_FILE_NAME}.qrc)
	set(QRC_CPP_CURRENT_FILE ${DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR}/qrc_${CURRENT_FILE_NAME}.cpp)

	set_property(SOURCE ${QRC_CPP_CURRENT_FILE} PROPERTY SKIP_AUTOMOC ON)
	target_sources(${PROJECT_NAME} PRIVATE ${QRC_CPP_CURRENT_FILE})

	add_custom_command(
		OUTPUT
			${QRC_QRC_CURRENT_FILE}
		COMMAND
			${DESIGN_TOKEN_CREATOR_EXE_BIN}
		ARGS
			${DESIGN_TOKEN_CREATOR_COMMAND_PARAMS} -F ${_file}
		COMMENT
			"[DesignToken::${PROJECT_NAME}] Creating QRC ${QRC_QRC_CURRENT_FILE} filename is ${CURRENT_FILE_NAME} EXT is ${CURRENT_FILE_NAME_WITH_EXT}"
		VERBATIM
		)

	add_custom_command(
		OUTPUT ${QRC_CPP_CURRENT_FILE}
		COMMAND
			${Qt5Core_RCC_EXECUTABLE}
		ARGS
			-name ${CURRENT_FILE_NAME} ${QRC_QRC_CURRENT_FILE} -o ${QRC_CPP_CURRENT_FILE}
		DEPENDS
			${QRC_QRC_CURRENT_FILE}
		COMMENT
			"[DesignToken::${PROJECT_NAME}] CREATING RES ${QRC_CPP_CURRENT_FILE}"
	)

	list(APPEND QRC_QRC_FILES ${QRC_CPP_CURRENT_FILE})

endforeach()


add_custom_command(
	OUTPUT
		${DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR}/${PROJECT_NAME}DesignToekn.stamp
	COMMAND
		${CMAKE_COMMAND} -E touch ${DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR}/${PROJECT_NAME}DesignToekn.stamp
	COMMENT
		"[DesignToken::${PROJECT_NAME}] Creating done"
	DEPENDS
		${QRC_QRC_FILES}
	VERBATIM
	)

add_custom_target(
	designToekn ALL
	DEPENDS ${DESIGN_TOKEN_CREATOR_COMMAND_PARAM_OUTPUT_DIR}/${PROJECT_NAME}DesignToekn.stamp
	)
