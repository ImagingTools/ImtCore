
#! Gets SDL generator executable path
macro(GetSdlGeneratorPath OUTPUT_SDL_GENERATOR_EXE_PATH)

	get_target_name(TARGETNAME)
	set(COMPILER_DIR ${CMAKE_BUILD_TYPE}_${TARGETNAME})
	string(FIND "$ENV{ARXCHOST}" "VC" POSITION_OF_VC)
	if (WIN32 OR (${POSITION_OF_VC} GREATER_EQUAL 0))
		set(SDL_GENERATOR_EXE_NAME "SdlCodeGenerator.exe")
	elseif(APPLE)
		set(SDL_GENERATOR_EXE_NAME "SdlCodeGenerator.app/Contents/MacOS/SdlCodeGenerator")
	else()
		set(SDL_GENERATOR_EXE_NAME "SdlCodeGenerator")
	endif()
	#  Setting the executable absolutely file path for specific OS
	if(ANDROID)
			set(SDL_GENERATOR_EXE_PATH "${CMAKE_CURRENT_FUNCTION_LIST_DIR}/../../Bin/${CMAKE_BUILD_TYPE}_$ENV{ARXCHOST}/${SDL_GENERATOR_EXE_NAME}")
	else()
			set(SDL_GENERATOR_EXE_PATH "${CMAKE_CURRENT_FUNCTION_LIST_DIR}/../../Bin/${COMPILER_DIR}/${SDL_GENERATOR_EXE_NAME}")
	endif()

	get_filename_component(SDL_GENERATOR_EXE_ABSOLUTE_PATH ${SDL_GENERATOR_EXE_PATH} ABSOLUTE)
	set(${OUTPUT_SDL_GENERATOR_EXE_PATH} ${SDL_GENERATOR_EXE_ABSOLUTE_PATH})
endmacro()

#! Generates code form SDL, and including provided deps to sources
function(ImtGenerateSdlWithDeps
	SDL_GENERATOR_COMMAND_PARAM_INPUT_FILE_PATH
	SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR
	SDL_GENERATOR_COMMAND_PARAM_NAMESPACE
	SDL_GENERATOR_COMMAND_PARAM_DEPSLIST
	SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS)

	GetSdlGeneratorPath(SDL_GENERATOR_EXE_PATH)
	message(VERBOSE "SDL_GENERATOR_EXE_PATH = ${SDL_GENERATOR_EXE_PATH}")
	message(VERBOSE "SDL_GENERATOR_COMMAND_PARAM_DEPSLIST = ${SDL_GENERATOR_COMMAND_PARAM_DEPSLIST}")

	add_custom_command(
		OUTPUT
			${SDL_GENERATOR_COMMAND_PARAM_DEPSLIST}
		COMMAND
			${SDL_GENERATOR_EXE_PATH}
		ARGS
			-GS ${SDL_GENERATOR_COMMAND_PARAM_INPUT_FILE_PATH} -O ${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR} -N ${SDL_GENERATOR_COMMAND_PARAM_NAMESPACE} ${SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS}
		DEPENDS
			${SDL_GENERATOR_EXE_PATH} ${SDL_GENERATOR_COMMAND_PARAM_INPUT_FILE_PATH}
		COMMENT
			"[SDL::${PROJECT_NAME}] Creating classes"
		VERBATIM
	)

	target_sources(${PROJECT_NAME} PRIVATE ${SDL_GENERATOR_COMMAND_PARAM_DEPSLIST})
endfunction()

#! Generates CPP(CXX) code form SDL schema
function(ImtGenerateCppSdl
		SDL_GENERATOR_COMMAND_PARAM_INPUT_FILE_PATH
		SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR
		SDL_GENERATOR_COMMAND_PARAM_NAMESPACE
		SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS)

	message(STATUS "SDL generation for ${PROJECT_NAME} TARGETNAME ${TARGETNAME}")
	message(VERBOSE "\t ${SDL_GENERATOR_COMMAND_PARAM_INPUT_FILE_PATH}")
	message(VERBOSE "\t ${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}")
	message(VERBOSE "\t ${SDL_GENERATOR_COMMAND_PARAM_NAMESPACE}")

	# Create SDL working dir if it does not exists
	if (NOT EXISTS "${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}")
		make_directory("${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}")
	endif()

	# Defile a dependencies list to be generated
	get_filename_component(SDL_SCHEMA_NAME "${SDL_GENERATOR_COMMAND_PARAM_INPUT_FILE_PATH}" NAME_WE)
	set(SDL_HEADER_JOINED_FILE_PATH "${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}/${SDL_SCHEMA_NAME}.h")
	set(SDL_SOURCE_JOINED_FILE_PATH "${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}/${SDL_SCHEMA_NAME}.cpp")
	list(APPEND SDL_DEPS "${SDL_HEADER_JOINED_FILE_PATH}")
	list(APPEND SDL_DEPS "${SDL_SOURCE_JOINED_FILE_PATH}")

	message(VERBOSE "SDL found deps for ${PROJECT_NAME}: ${SDL_DEPS}")
	list(APPEND SDL_NEW_GENERATOR_COMMAND_PARAM_MODIFICATORS "${SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS}")
	list(APPEND SDL_NEW_GENERATOR_COMMAND_PARAM_MODIFICATORS "-JH=${SDL_HEADER_JOINED_FILE_PATH}")
	list(APPEND SDL_NEW_GENERATOR_COMMAND_PARAM_MODIFICATORS "-JCPP=${SDL_SOURCE_JOINED_FILE_PATH}")

	ImtGenerateSdlWithDeps(
		${SDL_GENERATOR_COMMAND_PARAM_INPUT_FILE_PATH}
		${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}
		${SDL_GENERATOR_COMMAND_PARAM_NAMESPACE}
		"${SDL_DEPS}"
		"${SDL_NEW_GENERATOR_COMMAND_PARAM_MODIFICATORS}"
	)

endfunction()

#! Generates QML code form SDL schema
function(ImtGenerateQmlSdl
		SDL_GENERATOR_COMMAND_PARAM_INPUT_FILE_PATH
		SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR
		SDL_GENERATOR_COMMAND_PARAM_NAMESPACE
		SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS)

	message(STATUS "SDL generation for ${PROJECT_NAME} TARGETNAME ${TARGETNAME}")
	message(VERBOSE "\t ${SDL_GENERATOR_COMMAND_PARAM_INPUT_FILE_PATH}")
	message(VERBOSE "\t ${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}")
	message(VERBOSE "\t ${SDL_GENERATOR_COMMAND_PARAM_NAMESPACE}")

	# Create SDL working dir if it does not exists
	if (NOT EXISTS "${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}")
		make_directory("${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}")
	endif()

	# Defile a dependencies list to be generated
	get_filename_component(SDL_SCHEMA_NAME "${SDL_GENERATOR_COMMAND_PARAM_INPUT_FILE_PATH}" NAME_WE)
	set(SDL_QRC_FILE_PATH "${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}/${SDL_GENERATOR_COMMAND_PARAM_NAMESPACE}.qrc")
	set(SDL_QMLDIR_FILE_PATH "${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}/qmldir")
	list(APPEND SDL_DEPS "${SDL_QRC_FILE_PATH}")
	list(APPEND SDL_DEPS "${SDL_QMLDIR_FILE_PATH}")

	message(VERBOSE "SDL found deps for ${PROJECT_NAME}: ${SDL_DEPS}")

	ImtGenerateSdlWithDeps(
		${SDL_GENERATOR_COMMAND_PARAM_INPUT_FILE_PATH}
		${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}
		${SDL_GENERATOR_COMMAND_PARAM_NAMESPACE}
		"${SDL_DEPS}"
		"${SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS}"
	)

endfunction()

#! Generates code form SDL schema with separated files
function(ImtGenerateSdlSeparated
	SDL_GENERATOR_COMMAND_PARAM_INPUT_FILE_PATH
	SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR
	SDL_GENERATOR_COMMAND_PARAM_NAMESPACE
	SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS)

	message(STATUS "SDL generation for ${PROJECT_NAME} TARGETNAME ${TARGETNAME}")
	message(VERBOSE "\t ${SDL_GENERATOR_COMMAND_PARAM_INPUT_FILE_PATH}")
	message(VERBOSE "\t ${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}")
	message(VERBOSE "\t ${SDL_GENERATOR_COMMAND_PARAM_NAMESPACE}")

	# Create SDL working dir if it does not exists
	if (NOT EXISTS "${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}")
		make_directory("${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}")
	endif()
	
	GetSdlGeneratorPath(SDL_GENERATOR_EXE_PATH)

	# Generate a dependencies list to be generated
	set(SDL_DEPS_FILE_PATH "${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}/../__SDL__DependsList.txt")
	set(SDL_ERRORS_FILE_PATH "${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}/../__SDL__DependsList_errors.txt")
	set(SDL_DEPS_GENERATION_COMMAND "${SDL_GENERATOR_EXE_PATH} -DS ${SDL_GENERATOR_COMMAND_PARAM_INPUT_FILE_PATH} -O ${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR} ${SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS}")
	execute_process(
		COMMAND
			${SDL_GENERATOR_EXE_PATH} -DS ${SDL_GENERATOR_COMMAND_PARAM_INPUT_FILE_PATH} -N ${SDL_GENERATOR_COMMAND_PARAM_NAMESPACE} -O ${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR} ${SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS}
		OUTPUT_FILE
			${SDL_DEPS_FILE_PATH}
		ERROR_FILE
		${SDL_ERRORS_FILE_PATH}
		RESULT_VARIABLE
		SDL_DEPS_GENERATION_RESULT_CODE
	)

	if(NOT SDL_DEPS_GENERATION_RESULT_CODE EQUAL 0)
		message(WARNING "SDL Can't create dependency list")

		file(STRINGS ${SDL_DEPS_FILE_PATH} ERRORS1_SDL_DEPS_LIST)
		message(WARNING "${ERRORS1_SDL_DEPS_LIST}")

		file(STRINGS ${SDL_ERRORS_FILE_PATH} ERRORS2_SDL_DEPS_LIST)
		message(WARNING "${ERRORS2_SDL_DEPS_LIST}")

		message("EXEC: ${SDL_DEPS_GENERATION_COMMAND}")
		message(FATAL_ERROR "SDL finished unexpected. Error code: [${SDL_DEPS_GENERATION_RESULT_CODE}]")
	endif()

	file(STRINGS ${SDL_DEPS_FILE_PATH} SDL_GENERATED_DEPS)

	message(VERBOSE "SDL found deps for ${PROJECT_NAME}: ${SDL_GENERATED_DEPS}")

	# cleanup on success
	file(REMOVE ${SDL_DEPS_FILE_PATH})
	file(REMOVE ${SDL_ERRORS_FILE_PATH})

	ImtGenerateSdlWithDeps(
		${SDL_GENERATOR_COMMAND_PARAM_INPUT_FILE_PATH}
		${SDL_GENERATOR_COMMAND_PARAM_OUTPUT_DIR}
		${SDL_GENERATOR_COMMAND_PARAM_NAMESPACE}
		"${SDL_GENERATED_DEPS}"
		"${SDL_GENERATOR_COMMAND_PARAM_MODIFICATORS}"
	)

endfunction()


#! wrap for qt resources
function (ImtFutureResourceWrap
	WRAP_TARGET
	WRAP_RESOURCE_FILE)

	get_filename_component(CURRENT_FILE_NAME "${WRAP_RESOURCE_FILE}" NAME_WE)
	get_filename_component(CURRENT_FILE_PATH "${WRAP_RESOURCE_FILE}" DIRECTORY)
	set(CURRENT_FILE_DEST_PATH "${CURRENT_FILE_PATH}/_qrcWrap")
	file(MAKE_DIRECTORY "${CURRENT_FILE_DEST_PATH}")

	set(QRC_CPP_CURRENT_FILE ${CURRENT_FILE_DEST_PATH}/qrc_${CURRENT_FILE_NAME}.cpp)

	target_sources(${PROJECT_NAME} PRIVATE ${QRC_CPP_CURRENT_FILE})

	add_custom_command(
		OUTPUT ${QRC_CPP_CURRENT_FILE}
		COMMAND
		Qt${QT_VERSION_MAJOR}::rcc
		ARGS
			-name ${CURRENT_FILE_NAME} ${WRAP_RESOURCE_FILE} -o ${QRC_CPP_CURRENT_FILE}
		DEPENDS
			${WRAP_RESOURCE_FILE}
		COMMENT
			"[resource wrap::${WRAP_TARGET}] CREATING RES ${QRC_CPP_CURRENT_FILE}"
	)

	set(QML_IMPORT_DIRS ${QML_IMPORT_PATH})
	list(APPEND QML_IMPORT_DIRS "${CURRENT_FILE_PATH}/../")
	list(REMOVE_DUPLICATES QML_IMPORT_DIRS)
	set(QML_IMPORT_PATH ${QML_IMPORT_DIRS}
		CACHE STRING "Qt Creator extra qml import paths for ${PROJECT_NAME}"
		FORCE
	)

endfunction()




